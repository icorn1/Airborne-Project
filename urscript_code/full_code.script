# Run this code to turn on all of the hardware connected to the robot and to initialise the variables.
# Keep the sleep() or else the robot might give an error when moving on from digital output 2.
# In the set_standard_analog_out(0, 0.3) statement 0.3 can be changed from 0 to 1 to adjust the brightness of the lamp.

def Initialise():
 set_standard_analog_out(0, 0.3)
 set_standard_analog_out(1, 0.0)

 set_standard_digital_out(0, True)
 set_standard_digital_out(1, True)
 set_standard_digital_out(3, True)
 set_standard_digital_out(2, True)
 sleep(1)
 set_standard_digital_out(4, True)
 set_standard_digital_out(5, True)
 set_standard_digital_out(6, True)
 set_standard_digital_out(7, True)

 global START_LOC = p[-0.400, 0.000, 0.100, 3.14, 0, 0]
 global MOVE_DOWN = p[0,0,0.180,0,0,0]
 global MOVE_UP   = p[0,0,-0.180,0,0,0]
 global IP_HOST = "192.168.0.100"
 global PORT = 50001
 global PLY_ID = 3
 global Z_HEIGHT = 0.300  # in meters
 movej(START_LOC)
end


# Run this code on the robot to de-initialise everything and turn off all the digital output pins.

def De_initialise():
 set_standard_analog_out(0, 0.0)
 set_standard_analog_out(1, 0.0)
 set_standard_digital_out(0, False)
 set_standard_digital_out(1, False)
 set_standard_digital_out(2, False)
 set_standard_digital_out(3, False)
 set_standard_digital_out(4, False)
 set_standard_digital_out(5, False)
 set_standard_digital_out(6, False)
 set_standard_digital_out(7, False)
 movej(START_LOC)
end



# Discription to function calibration()
# This function first moves the robot to an appropriate location from where to start
# the rest of all the actions. The function opens a connection to Host, this is the 
# PC used. It sends the command "clibration" to the PC so that the PC knows which 
# function is being run. The function gets an error code 0 or 1 from the PC, 0 means
# that a new pose can be send by the PC, 1 means that no poses can be send, this happens
# when all poses have been done already. If the error code is 0 the robot will move
# to the pose it gets from the PC and then send to the PC that the PC can make a
# picture. The robot waits for a while to give time to the PC to make the picture

def calibration():
 movej(p[-0.400, 0.000, 0.300, 0, 0, 0])

 while socket_open(IP_HOST, PORT) == True:
  socket_send_string("calibration")
  error = socket_read_ascii_float(1, "socket_0")

  if error[1] == 1:
   movej(p[-0.250, -0.570, 0.300, 0, 0, 0])
   movej(p[-0.250, -0.570, 0.300, 0.93, 1.16, 0])
   movej(p[-0.250, -0.570, 0.300, 2.0, 2.5, 0])
   socket_close("socket_0")
   break
  end

  if error[1] == 0:
   data = socket_read_ascii_float(6, "socket_0")
   calibration_location = p[data[1], data[2], data[3], data[4], data[5], data[6]]
   movej(calibration_location)
   socket_send_string("ready for picture")
   sleep(2)
   socket_close("socket_0")
  end
 end
end

# the positions need to be addapted for the calibration tool used.
#calibration()


# Discription to function start_moving_ply()
# This function opens a connection to Host, this is the PC used. It sends the command 
# "moving" to the PC so that the PC knows which function is being run. The ply ID is
# send to the PC. The PC will send the location of the ply with the right ply ID back.
# If the ply is not found, an error_code of 1 is returned by the PC and the UR will go
# to the GUI and there it will ask if you want to skip the ply or chose a new one to fill
# the place of the missing ply. when the robot is at the ply (error_code = 0) it will
# send a command to the PC to activate the tool end, which consists of vacu√ºm cups.
# After this the robot moves to where the composite should be build.

def start_moving_ply():
 if socket_open(IP_HOST, PORT) == True:
  socket_send_string("moving")

  socket_send_string(PLY_ID)
  data = socket_read_ascii_float(5, "socket_0")

  if data[5] == 0:
   textmsg(str_cat("error_code: ", data[5]))
   
   ply_location  = p[data[1], data[2], Z_HEIGHT, data[3], data[4], 0]
   composite_loc = p[-0.4,0.5,Z_HEIGHT,3.14,0,0]
   
   textmsg("position: ", ply_location)
   movej(ply_location)
   movej(pose_trans(ply_location, MOVE_DOWN))
   socket_send_string("activate")
   sleep(0.3)
   movej(ply_location)
   
   #check if there is a ply there
   sensor = get_standard_analog_in(1)
   
   if sensor < 0.1:
    # There is a ply attached
    movej(composite_loc)
    socket_send_string("Deactivate")
   end

   if sensor > 0.1:
    # There is no ply attached
    socket_send_string("Deactivate")

    # display a message on the GUI that says "failed to pick up ply, move ply to a different location"
    textmsg("failed to pick up ply")
   end
  end

  if data[5] == 1:
   textmsg("error_code: 1")
   # go back to GUI. GUI asks if you need to skip the ply or if you want to select another ply to fill in.
  end
  socket_close("socket_0")
 end
end


Initialise()


# the positions need to be addapted for the calibration tool used.
#calibration()

#start_moving_ply()
#halt


De_initialise()